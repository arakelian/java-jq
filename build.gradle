// java-jq

buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'eclipse'
    id 'idea'

    // keep dependencies up-to-date!
    id 'com.github.ben-manes.versions' version '0.20.0'

    // useful for creating immutable java beans
    id 'org.inferred.processors' version '1.2.18'

    // to ensure clean code
    id "net.ltgt.errorprone" version "0.0.16"

    // for deployment to Maven Central
    id "io.codearte.nexus-staging" version "0.11.0"

    // for building fat jar
    id "com.github.johnrengelman.shadow" version "2.0.4"

    // useful utilities
    id "com.arakelian.multitool" version "1.5.3"
}

group   = 'com.arakelian'

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.2'
}

multitool {
    relocates += [
        'com.google.common'      : 'repackaged.com.arakelian.jq.${prefix}com.google.common',
        'com.google.thirdparty'  : 'repackaged.com.arakelian.jq.${prefix}com.google.thirdparty',
        'javax.annotation'       : 'repackaged.com.arakelian.jq.${prefix}javax.annotation',
    ]
    
    proguardConfiguration {
        keep includedescriptorclasses:true, 'class com.arakelian.** { *; }'

        dontnote '**.org.apache.commons.lang3.ObjectUtils'
    }
}

dependencies {
    processor 'org.immutables:value:2.7.1'

    // we shadow Guava due to frequent downstream compatibility issues
    shadow 'com.google.guava:guava:26.0-jre'

    // we do not shadow this library because there may be linkages to native code that
    // rely upon specific package or class names
    compile 'net.java.dev.jna:jna:4.5.2'

    // for unit testing
    testCompile 'junit:junit:4.12'
}
