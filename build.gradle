// java-jq

buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'eclipse'
    id 'idea'

    // keep dependencies up-to-date!
    id 'com.github.ben-manes.versions' version '0.17.0'

    // useful for creating immutable java beans
    id 'org.inferred.processors' version '1.2.15'

    // to ensure clean code
    id "net.ltgt.errorprone" version "0.0.13"

    // for deployment to Maven Central
    id "io.codearte.nexus-staging" version "0.11.0"

    // for building fat jar
    id "com.github.johnrengelman.shadow" version "2.0.2"

    // useful utilities
    id "com.arakelian.multitool" version "1.3.0"
}

group   = 'com.arakelian'

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}

multitool {
    relocates += [
        'com.google.common'      : 'repackaged.com.arakelian.jq.${prefix}com.google.common',
        'com.google.thirdparty'  : 'repackaged.com.arakelian.jq.${prefix}com.google.thirdparty',
        'javax.annotation'       : 'repackaged.com.arakelian.jq.${prefix}javax.annotation',
    ]
}

dependencies {
    processor 'org.immutables:value:2.5.6'

    // we shadow Guava due to frequent downstream compatibility issues
    shadow 'com.google.guava:guava:24.0-jre'

    // we do not shadow this library because there may be linkages to native code that
    // rely upon specific package or class names
    compile 'net.java.dev.jna:jna:4.5.1'

    // logging
    testCompile 'org.apache.logging.log4j:log4j-api:2.9.1'
    testCompile 'org.apache.logging.log4j:log4j-core:2.9.1'
    testCompile 'org.apache.logging.log4j:log4j-slf4j-impl:2.9.1'
    testCompile 'org.slf4j:jcl-over-slf4j:1.7.25'
    testCompile 'org.slf4j:jul-to-slf4j:1.7.25'
    compile     'org.slf4j:slf4j-api:1.7.25'

    // for unit testing
    testCompile 'junit:junit:4.12'
}
